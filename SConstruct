import os
from templates import Manuscript

env = Environment()
env.SetDefault(TEXMFHOME=os.path.join(os.environ['HOME'], 'texmf'))
# To include the class and bibtex files in the 'classes' dir
env.Replace(TEXINPUTS='classes//:')
env.Replace(BSTINPUTS='classes//:')

builder = Manuscript(
    title='Optimal forward modeling of gravitational fields using tesseroids with adaptive discretization',
    author=['Leonardo Uieda',
            'Vanderlei C. Oliveira Jr',
            r'Val\'eria C. F. Barbosa'],
    affil=['Universidade do Estado do Rio de Janeiro, Rio de Janeiro, Brazil.',
           r'Observat\'orio Nacional, Rio de Janeiro, Brazil.'],
    author_affil=[[0, 1], [1], [1]],
    journal='gji',
    heads=['Uieda et al.', 'Optimal forward modeling using tesseroids'],
    bibfile='references.bib',
    pretty=False)
ms = env.Command('manuscript.tex', 'body.tex', builder.build)
pdf = env.PDF(target='manuscript.pdf', source=ms)
# Declare that the PDf depends on the figures so it gets rebuild when they
# change.
Depends(pdf, Glob('figs/*.eps'))
# These files get generated by pdflatex
Clean(os.path.curdir, Glob('*-eps-converted-to.pdf'))

Clean(os.path.curdir, Glob('*.pyc'))
